apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

Properties props = new Properties()
File propsFile = file('../secret.properties')
if (propsFile.exists()) {
    props.load(propsFile.newDataInputStream())
} else {
    props.setProperty("youtubeApiKey", "")
    props.setProperty("websiteUrl", "")
}

android {
    compileSdkVersion androidVersion

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion androidVersion

        buildConfigField "String", "YOUTUBE_API_KEY", "\"${props.getProperty("youtubeApiKey")}\""
        resValue "string", "website_url", "${props.getProperty("websiteUrl")}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation project(':androidcommon')
    implementation project(':feedback')
    implementation 'com.pacioianu.david:ink-page-indicator:1.3.0'
}
