apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'
apply plugin: 'kotlin-kapt'

Properties props = new Properties()
File propsFile = file('../secret.properties')
if (propsFile.exists()) {
    props.load(propsFile.newDataInputStream())
} else {
    props.setProperty("gaTrackingId", "")
    props.setProperty("aircraftBaseUrl", "")
}

android {
    compileSdkVersion androidVersion
    buildToolsVersion toolsVersion

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion androidVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GA_TRACKING_ID", "\"${props.getProperty("gaTrackingId")}\""
        buildConfigField "String", "AIRCRAFT_BASE_URL", "\"${props.getProperty("aircraftBaseUrl")}\""
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        force "io.reactivex:rxjava:$rxJavaVersion"
        force "com.android.support:support-annotations:$supportLibraryVersion"
        force "com.android.support:support-compat:$supportLibraryVersion"
        force "com.android.support:support-core-utils:$supportLibraryVersion"
        force "com.android.support:support-fragment:$supportLibraryVersion"
        force "com.android.support:support-core-ui:$supportLibraryVersion"
        force "com.google.firebase:firebase-analytics:16.0.3"
        force "com.google.firebase:firebase-common:16.0.1"
        force "com.android.support:animated-vector-drawable:$supportLibraryVersion"
        force "com.android.support:support-media-compat:$supportLibraryVersion"
        force "com.android.support:support-v4:$supportLibraryVersion"
    }
}

dependencies {
    api project(':recognition')

    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    api "com.android.support:appcompat-v7:$supportLibraryVersion"
    api "com.android.support:design:$supportLibraryVersion"
    api "com.android.support:percent:$supportLibraryVersion"
    api "android.arch.core:runtime:1.1.1"
    api 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation "com.google.firebase:firebase-appindexing:16.0.1"
    releaseImplementation "com.crashlytics.sdk.android:crashlytics:2.9.5"
    implementation "com.google.firebase:firebase-perf:16.1.0"
    implementation "com.google.android.gms:play-services-analytics:16.0.3"

    api 'io.reactivex:rxandroid:1.2.1'
    api 'com.jakewharton.rxbinding:rxbinding-kotlin:1.0.1'
    api 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation 'com.github.bumptech.glide:glide:3.7.0'

    api 'com.github.chrisbanes:PhotoView:2.0.0'

    debugImplementation 'com.facebook.stetho:stetho:1.4.1'
    //debugCompile 'com.uphyca:stetho_realm:2.0.0'

    testImplementation project(':testcommon')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

repositories {
    mavenCentral()
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
}

